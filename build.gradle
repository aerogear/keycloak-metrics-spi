import java.text.SimpleDateFormat

plugins {
    id "net.nemerosa.versioning" version "3.0.0"
    id "java"
}

configurations {
    bundleLib
}

group 'org.jboss.aerogear'
version '6.0.1-SNAPSHOT'


java {
    sourceCompatibility = JavaVersion.VERSION_17
}

repositories {
    mavenCentral()
}

ext {
    keycloakVersion=project.properties["keycloakVersion"]
    quarkusResteasyVersion=project.properties["quarkusResteasyVersion"]
    prometheusVersion=project.properties["prometheusVersion"]
}


dependencies {
    implementation group: 'org.keycloak', name: 'keycloak-server-spi-private', version: keycloakVersion
    implementation group: 'org.keycloak', name: 'keycloak-server-spi', version: keycloakVersion
    implementation group: 'org.keycloak', name: 'keycloak-services', version: keycloakVersion
    compileOnly group: 'io.quarkus.resteasy.reactive', name: 'resteasy-reactive', version: quarkusResteasyVersion
    bundleLib group: 'io.prometheus', name: 'simpleclient_common', version: prometheusVersion
    bundleLib group: 'io.prometheus', name: 'simpleclient_hotspot', version: prometheusVersion
    bundleLib group: 'io.prometheus', name: 'simpleclient_pushgateway', version: prometheusVersion
    configurations.implementation.extendsFrom(configurations.bundleLib)

    testImplementation group: 'junit', name: 'junit', version: '4.13'
    testImplementation group: 'uk.org.webcompere', name: 'system-stubs-junit4', version: '2.0.2'
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '5.4.0'
    // required by 'system-stubs-junit4'
    testImplementation group: 'org.mockito', name: 'mockito-inline', version: '5.2.0'

    compileOnly 'org.apache.tomcat:tomcat-servlet-api:9.0.37'

}

jar {
    from {
        configurations.bundleLib.collect { it.isDirectory() ? it : zipTree(it) }
    }
    manifest {
        attributes(
            'Build-Revision' : versioning.info.commit,
            'Version'        : "${gradle.rootProject.version}"
        )
    }
}

tasks.withType(AbstractArchiveTask.class).configureEach {
    preserveFileTimestamps = false
    reproducibleFileOrder = true
}
