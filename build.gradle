import java.text.SimpleDateFormat

plugins {
    id "net.nemerosa.versioning" version "3.0.0"
}

repositories {
    jcenter()
}

configurations {
    bundleLib
}

group 'org.jboss.aerogear'
version '3.0.1-SNAPSHOT'

apply plugin: 'java'

sourceCompatibility = 11

repositories {
    mavenCentral()
}

ext {
    keycloakVersion=project.properties["keycloakVersion"]
    prometheusVersion=project.properties["prometheusVersion"]
}


dependencies {
    implementation group: 'org.keycloak', name: 'keycloak-server-spi-private', version: keycloakVersion
    implementation group: 'org.keycloak', name: 'keycloak-server-spi', version: keycloakVersion
    implementation group: 'org.keycloak', name: 'keycloak-services', version: keycloakVersion
    implementation group: 'org.jboss.spec.javax.ws.rs', name: 'jboss-jaxrs-api_2.0_spec', version: '1.0.0.Final'
    bundleLib group: 'io.prometheus', name: 'simpleclient_common', version: prometheusVersion
    bundleLib group: 'io.prometheus', name: 'simpleclient_hotspot', version: prometheusVersion
    bundleLib group: 'io.prometheus', name: 'simpleclient_pushgateway', version: prometheusVersion
    configurations.implementation.extendsFrom(configurations.bundleLib)

    testImplementation group: 'junit', name: 'junit', version: '4.12'
    testImplementation group: 'com.github.stefanbirkner', name: 'system-rules', version: '1.19.0'
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '4.7.0'

    compileOnly 'org.apache.tomcat:tomcat-servlet-api:9.0.37'

}

jar {
    from {
        configurations.bundleLib.collect { it.isDirectory() ? it : zipTree(it) }
    }
    manifest {
        attributes(
            'Build-Revision' : versioning.info.commit,
            'Version'        : "${gradle.rootProject.version}"
        )
    }
}

tasks.withType(AbstractArchiveTask.class).configureEach {
    preserveFileTimestamps = false
    reproducibleFileOrder = true
}
