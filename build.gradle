import java.text.SimpleDateFormat

plugins {
    id "net.nemerosa.versioning" version "2.8.2"
}

repositories {
    jcenter()
}

configurations {
    bundleLib
}

group 'org.jboss.aerogear'
version '2.5.3-SNAPSHOT'

apply plugin: 'java'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

ext {
    keycloakVersion=project.properties["keycloakVersion"]
    prometheusVersion=project.properties["prometheusVersion"]
}


dependencies {
    implementation group: 'org.keycloak', name: 'keycloak-server-spi-private', version: keycloakVersion
    implementation group: 'org.keycloak', name: 'keycloak-server-spi', version: keycloakVersion
    implementation group: 'org.keycloak', name: 'keycloak-services', version: keycloakVersion
    implementation group: 'org.jboss.spec.javax.ws.rs', name: 'jboss-jaxrs-api_2.0_spec', version: '1.0.0.Final'
    bundleLib group: 'io.prometheus', name: 'simpleclient_common', version: prometheusVersion
    bundleLib group: 'io.prometheus', name: 'simpleclient_hotspot', version: prometheusVersion
    bundleLib group: 'io.prometheus', name: 'simpleclient_pushgateway', version: prometheusVersion
    configurations.implementation.extendsFrom(configurations.bundleLib)

    // Keycloak 16.0.0 comes with Wildfly 25 that brings RestEasy v4
    implementation group: 'org.jboss.resteasy', name:'resteasy-multipart-provider', version: '4.7.3.Final'

    testImplementation group: 'junit', name: 'junit', version: '4.12'
    testImplementation group: 'com.github.stefanbirkner', name: 'system-rules', version: '1.19.0'

    compileOnly 'org.apache.tomcat:tomcat-servlet-api:9.0.37'

}

jar {
    from {
        configurations.bundleLib.collect { it.isDirectory() ? it : zipTree(it) }
    }
    manifest {
        attributes(
            'Built-By'       : System.properties['user.name'],
            'Build-Timestamp': new SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZ").format(new Date()),
            'Build-Revision' : versioning.info.commit,
            'Created-By'     : "Gradle ${gradle.gradleVersion}",
            'Build-Jdk'      : "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
            'Build-OS'       : "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}",
            'Version'        : "${gradle.rootProject.version}"
        )
    }
}
